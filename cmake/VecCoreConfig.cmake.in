@PACKAGE_INIT@

set(VecCore_FOUND TRUE)

set(VecCore_VERSION @VecCore_VERSION@)
set(VecCore_VERSION_MAJOR @VecCore_VERSION_MAJOR@)
set(VecCore_VERSION_MINOR @VecCore_VERSION_MINOR@)
set(VecCore_VERSION_PATCH @VecCore_VERSION_PATCH@)

set_and_check(VecCore_CMAKE_DIR "${CMAKE_CURRENT_LIST_DIR}")
set_and_check(VecCore_INCLUDE_DIR "@PACKAGE_CMAKE_INSTALL_INCLUDEDIR@")

set(VecCore_INCLUDE_DIRS "${VecCore_INCLUDE_DIR}")

list(APPEND CMAKE_MODULE_PATH "${VecCore_CMAKE_DIR}")

include(CMakeFindDependencyMacro)

if (VecCore_FIND_COMPONENTS MATCHES "CUDA")
  include(CheckLanguage)
  check_language(CUDA)
  if(CMAKE_CUDA_COMPILER)
    enable_language(CUDA)
    set(VecCore_CUDA_FOUND True)
    set(VecCore_CUDA_DEFINITIONS -DVECCORE_ENABLE_CUDA)
    set(VecCore_CUDA_INCLUDE_DIR ${CMAKE_CUDA_TOOLKIT_INCLUDE_DIRECTORIES})
  endif()
endif()

if (VecCore_FIND_COMPONENTS MATCHES "UMESIMD")
  find_dependency(UMESIMD 0.8.1)
  if(UMESIMD_FOUND)
    set(VecCore_UMESIMD_FOUND True)
    set(VecCore_UMESIMD_DEFINITIONS -DVECCORE_ENABLE_UMESIMD)
    set(VecCore_UMESIMD_INCLUDE_DIR ${UMESIMD_INCLUDE_DIRS})
  endif()
endif()

if (VecCore_FIND_COMPONENTS MATCHES "Vc")
  find_dependency(Vc 1.3.3 CONFIG)
  if (Vc_FOUND)
    set(VecCore_Vc_FOUND True)
    set(VecCore_Vc_DEFINITIONS -DVECCORE_ENABLE_VC)
    set(VecCore_Vc_INCLUDE_DIR ${Vc_INCLUDE_DIR})
    set(VecCore_Vc_LIBRARIES ${VecCore_LIBRARIES} ${Vc_LIBRARIES})
  endif()
endif()

check_required_components(VecCore)

foreach(component ${VecCore_FIND_COMPONENTS})
  if("${component}" MATCHES "CUDA|UMESIMD|Vc")
    set(VecCore_DEFINITIONS ${VecCore_DEFINITIONS} ${VecCore_${component}_DEFINITIONS})
    set(VecCore_INCLUDE_DIRS ${VecCore_INCLUDE_DIRS} ${VecCore_${component}_INCLUDE_DIR})
    set(VecCore_LIBRARIES ${VecCore_LIBRARIES} ${VecCore_${component}_LIBRARIES})
  else()
    set(VecCore_FOUND False)
    set(_VecCore_NOT_FOUND_MESSAGE
      "Unknown component: ${component} (known components: CUDA, UMESIMD, Vc)")
    return()
  endif()
endforeach()

list(REMOVE_DUPLICATES VecCore_INCLUDE_DIRS)

if (VecCore_FOUND AND NOT TARGET VecCore::VecCore)
  add_library(VecCore::VecCore INTERFACE IMPORTED)
  set_target_properties(VecCore::VecCore PROPERTIES
    INTERFACE_INCLUDE_DIRECTORIES "${VecCore_INCLUDE_DIRS}")

  if (VecCore_DEFINITIONS)
    string(REGEX REPLACE "-D" "" _localdefs "${VecCore_DEFINITIONS}")
    set_target_properties(VecCore::VecCore PROPERTIES
      INTERFACE_COMPILE_DEFINITIONS "${_localdefs}")
    unset(_localdefs)
  endif()

  if (VecCore_LIBRARIES)
    set_target_properties(VecCore::VecCore PROPERTIES
      INTERFACE_LINK_LIBRARIES "${VecCore_LIBRARIES}")
  endif()
endif()
